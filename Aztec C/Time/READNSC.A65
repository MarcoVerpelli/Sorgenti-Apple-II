         instxt "zpage.h"

clrrom   equ $CFFF
writensc equ $C300 ;assume NSC in slot #3 (OK for AppleWin emulator)
readnsc  equ writensc+4

         public readnsc_

readnsc_

         ldy #2
         lda (SP),y
         sta R1
         iny
         lda (SP),y
         sta R1+1

         php
         sei
         lda clrrom
         pha
         sta writensc
         lda readnsc
         ldx #7
writepat lda pattern,x
         sec
         ror
l1       pha
         lda #$00
         rol
         tay
         lda writensc,y
         pla
         lsr
         bne l1
         dex
         bpl writepat
         ldy #7
readclk  ldx #7
l2       pha
         lda readnsc
         ror
         pla
         ror
         dex
         bpl l2
         sta (R1),y
         dey
         bpl readclk
         pla
         bmi l3
         sta clrrom
l3       plp
         rts

pattern  fcb $5C,$A3,$3A,$C5,$5C,$A3,$3A,$C5
